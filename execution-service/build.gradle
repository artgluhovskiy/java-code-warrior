buildscript {
    ext {
        springBootVersion = '2.1.2.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

plugins {
    id 'net.saliman.cobertura' version '2.6.0'
}

ext {
    springBootVersion = '2.1.2.RELEASE'
    targetResourceDir = '/docker'
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

sourceCompatibility = 1.8
targetCompatibility = 1.8

bootJar {
    baseName = 'execution-service-boot'

    //Hack to load the class (which should be seen by the System Compiler) via system class loader rather than Spring Boot classloader by copying class file to the root of boot jar
    with copySpec {
        from "$projectDir/../commons/build/classes/java/main/org/art/web/warrior/commons/driver/exception"
        into 'org/art/web/warrior/commons/driver/exception'
    }
}

repositories {
    mavenCentral()
    mavenLocal()
}

buildScan {
    termsOfServiceUrl = 'https://gradle.com/terms-of-service'
    termsOfServiceAgree = 'yes'
}

test {
    useJUnitPlatform()
    testLogging {
        showStandardStreams = true
    }
}

dependencies {

    implementation project(':commons')

    /* *** Spring *** */
    implementation('org.springframework.boot:spring-boot-starter')
    implementation('org.springframework.boot:spring-boot-starter-web')

    implementation('org.springframework.cloud:spring-cloud-starter-netflix-eureka-client')

    testImplementation('org.springframework.boot:spring-boot-starter-test')

    /* *** Common Libs *** */
    implementation('com.esotericsoftware:kryo:4.0.0')
    implementation('org.apache.commons:commons-lang3:3.1')
    implementation('org.apache.commons:commons-collections4:4.0')

    implementation('ch.qos.logback:logback-core:1.2.3')
    implementation('org.slf4j:slf4j-api:1.7.26')

    implementation('org.projectlombok:lombok:1.18.4')
    annotationProcessor("org.projectlombok:lombok:1.18.4")

    /* *** TDD *** */
    testImplementation('org.junit.jupiter:junit-jupiter-api:5.3.1')
    testRuntimeOnly('org.junit.jupiter:junit-jupiter-engine:5.3.1')

    testImplementation('org.mockito:mockito-junit-jupiter:2.18.3')
    testImplementation('org.mockito:mockito-core:2.+')
}

dependencyManagement {
    imports {
        mavenBom 'org.springframework.cloud:spring-cloud-dependencies:Greenwich.RELEASE'
    }
}

task collectResourcesToDir(type: Copy, dependsOn: build) {
    println "Copying project resources to the directory '$targetResourceDir'"
    from "$buildDir/libs", "$projectDir/src/main/resources"
    include "*.jar", "*.sh"
    into "$projectDir$targetResourceDir"
}

task runBuild(dependsOn: collectResourcesToDir) {
    println 'Running build!'
}

task cleanProject(type: Delete, dependsOn: clean) {
    delete "$projectDir$targetResourceDir"
}